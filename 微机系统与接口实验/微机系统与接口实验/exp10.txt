;计数
DAT SEGMENT
    LEDTABLE 	DB 3FH,06H,5BH,4FH,66H,6DH,7DH,07H,7FH,6FH,77H,7CH,39H,5EH,79H,71H,00H;0~F
    DNUM    DB 10H,10H,10H,10H  ;LED显示内容 
    SNUM    DB 00H,00H ;LED应显示的数字
    ANUM    DB 10H  ;键盘读入字符
    TIMENUM DB 00H,00H  ;读入分钟数,两位BCD码
    TIMERET DW 0000H,0000H,0000H,0000H ;存放废弃的time up返回地址
    FLAG    DB 00H;输入完标志
    
    TONUM   EQU 4;LED个数 
    MAXR    EQU 3;最大读键盘次数
    LEFTLED EQU 11111110B;最左LED的位选码 
    
    PA8255  EQU 0600H;IOY0
    PB8255  EQU 0602H
    PC8255  EQU 0604H
    CON8255 EQU 0606H
    
    OUT0_8254 EQU 0640H;IOY1
    OUT1_8254 EQU 0642H
    OUT2_8254 EQU 0644H
    CON_8254  EQU 0646H 
    
    A_8259  EQU 0020H
    B_8259  EQU 0021H
    ICW2_8259 EQU 08H
    MIR6_8259 EQU 38H;MIR6接OUT1口每1秒减少一次数值			MIR6的矢量地址
    MIR7_8259 EQU 3CH;MIR7接OUT0口每0.25秒刷新一次LED			MIR7的矢量地址
    	
    MINUTE  EQU  4800H;1毫秒钟计数值
    RETIME  EQU  4800H;LED刷新时间1毫秒
DAT ENDS
COD SEGMENT
        ASSUME CS:COD,DS:DAT
START:  MOV AX,DAT
        MOV DS,AX  
        
        CALL INIT8255;初始化8255
        CALL INIT8259;初始化8259
        CALL INITTIME;初始化计时器
        ;初始化状态
S0:     CALL PAUSETIME;停止计数
        CALL INITSTATES;初始化LED状态
        
        MOV FLAG,00H
        ;初值状态
        MOV  CX,2;输入两位初值
S1:     ;CALL DELAYS
        CALL DISPLAYER;延时
        MOV  ANUM,10H
        CALL INPUT
        MOV  AL,ANUM
        CMP  AL,0CH
        JZ   SZ11
        CMP  AL,0AH				;如果等于A表示启动
        JZ   S2_1
        CMP  AL,09H
        JA   S1 				;JA：AL>09H跳转，即表示拒接接收大于C的数字
        CALL SHIFTNUM			;DNUM中数字向左移一,ANUM中数字加入最右边一位，本来DNUM中存的是下标，现在存的就是要显示的数字了
        LOOP S1				;获得两位起始的分钟数
        JMP S2
SZ11:   JMP NEAR PTR SZ  

S2:     ;等待状态
        ;CALL DELAYS 
        CALL DISPLAYER
        ;读入
        MOV  ANUM,10H
        CALL INPUT					;input 表示获得键盘输入，修改ANUM
        MOV  AL,ANUM;此时AL存的是键盘键入的数字
        CMP  AL,0AH;比较AL是不是10，如果是10（A键），则表示启动/取消
        JZ   S2_1
        CMP  AL,0CH
        JZ   SZ
        JMP  S2
S2_1:   CALL INPUT_TO_NUM;转化为数字			读入起始分钟数
		MOV DL,TIMENUM			;获得分钟第一位
		MOV CL,4	
        ROL DL,CL					;循环左移四位
        AND DL,0F0H					;保留高四位
        MOV DH,TIMENUM+1				;获得分钟第二位
        AND DH,0FH					;保留低四位
        OR  DL,DH				
        CMP DL,00H					;当DL高四位与DH低四位都是0时或运算结果才为0，此时表示还没有获得起始时间
        JZ S0
		CALL STATESTO0;置初值 		将TIMENUM中的分钟放到DNUM前两位，DNUM后两位放00H，设置倒计时的起始时间
        CALL STIME					;开始计时 
        
        ;计时并等待输入状态
S3:     ;CALL DELAYS
        CALL DISPLAYER				;延时
        MOV  ANUM,10H ;读入
        CALL INPUT
        MOV  AL,ANUM				;获得键盘输入
        CMP  AL,0AH
        JZ   SS0       					;输入为A表示取消
        CMP  AL,0BH
        JZ   S4					;输入为B暂停
        CMP  AL,0CH
        JZ   SZ					;输入为C表示停止
        JMP  S3
SS0:    JMP FAR PTR S0        
        ;取消状态
S4:     CALL PAUSETIME;暂停计时 
S5:     ;CALL DELAYS					;处理按下B（暂停）之后的键盘输入
        CALL DISPLAYER
        ;读入
        MOV  ANUM,10H 
        CALL INPUT
        MOV  AL,ANUM
        CMP  AL,0AH
        JZ   S5_0      
        CMP  AL,0BH
        JZ   S6
        CMP  AL,0CH
        JZ   SZ
        JMP  S5
S6:     CALL STIME					;重新开始计时
        JMP  S3
        
S5_0:   JMP NEAR PTR S0
          
        ;停止状态
SZ:     ;CALL PAUSETIME		;初始化计时器      
        ;CALL INITSTATES		;初始化LED状态
        CALL EXIT
EXIT:   MOV AH,4CH
        INT 21H
        ;主程序结束

INPUT_TO_NUM PROC NEAR				;输入转化为数字，将DNUM的后两位放到TIMENUM中，此时DNUM中的数字由ANUM得到，也就是要显示的数字
		PUSH BX
        PUSH CX
        PUSH DX
        
        LEA BX,DNUM
        MOV DL,TONUM-2[BX]
        CMP	DL,10H
        JNZ ITN1
        MOV DL,00H
ITN1:   MOV TIMENUM,DL 
        MOV DH,TONUM-1[BX]
        MOV TIMENUM+1,DH
        
        POP DX
        POP CX
        POP BX    
        RET
INPUT_TO_NUM ENDP  

      
FLASH_3_TIMES PROC NEAR;闪烁三次
        CLI 							;关中断
        PUSH CX
        PUSH DX
        
        MOV CX,3
         
FLASH:  CALL INITSTATES					;将DNUM全置为10H
        MOV DX,01FFH  

P0:		CALL DISPLAYS				;将DNUM的内容显示到数码管上，此时DNUM的内容是作为下标访问的，即此时数码管上不显示（00H）
        DEC DX
		JNZ P0
        
        
       CALL INITSTATE0					;将DNUM全置为00H
       MOV DX,01FFH  

P1:		CALL DISPLAYS				;将DNUM的内容显示到数码管上，此时DNUM的内容是作为下标访问的，此时数码管显示0
		DEC DX
		JNZ P1					;长时间显示
        
        
        LOOP FLASH
        
        CALL INITSTATES					;将DNUM全置为10H
        MOV DX,01FFH  

P2:		CALL DISPLAYS				;将DNUM的内容显示到数码管上，此时DNUM的内容是作为下标访问的，即此时数码管上不显示（00H）
		DEC DX
		JNZ P2					;长时间不显示
        
        POP DX
        POP CX
        STI     
        RET
FLASH_3_TIMES ENDP        
           
STATESTO0 PROC NEAR;LED置初值				;将TIMENUM中的分钟放到DNUM前两位，DNUM后两位放00H，设置倒计时的起始时间
        PUSH AX		
        PUSH BX 
        
        LEA BX,DNUM
        MOV AL,0[TIMENUM]
        MOV 0[BX],AL
        MOV AL,1[TIMENUM]
        MOV 1[BX],AL
        MOV BYTE PTR 2[BX],00H
        MOV BYTE PTR 3[BX],00H
        
        POP BX
        POP AX
        RET
STATESTO0 ENDP

INITSTATE0 PROC NEAR;LED置全0			将DNUM全置为00H
        PUSH BX 
        
        LEA BX,DNUM
        MOV BYTE PTR 0[BX],00H		;因为不是寄存器所以要有BYTE PTR
        MOV BYTE PTR 1[BX],00H
        MOV BYTE PTR 2[BX],00H
        MOV BYTE PTR 3[BX],00H
        
        POP BX
        RET
INITSTATE0 ENDP

INITSTATES PROC NEAR;初始化LED状态			将DNUM全置为10H
        PUSH BX 
        
        LEA BX,DNUM
        MOV BYTE PTR 0[BX],10H  
        MOV BYTE PTR 1[BX],10H
        MOV BYTE PTR 2[BX],10H
        MOV BYTE PTR 3[BX],10H
        
        POP BX
        RET
INITSTATES ENDP

INIT8255 PROC NEAR 
        PUSH AX
        PUSH DX
        
        ;A口输出，B口输出，C口高4位输出(计数器GATE)，C口低4位输入
        MOV DX,CON8255 ;INIT 8255
        MOV AL,10000001B;81H
        OUT DX,AL
        
        POP DX
        POP AX
        RET
INIT8255 ENDP

INIT8259 PROC NEAR			;初始化8259，设置中断向量 
        PUSH AX
        PUSH DX
        
        CLI
        MOV DX,A_8259 ;8259 ICW1
        MOV AL,00010011B			;IR为上升沿触发，08259A为单片使用，需要ICW4
        OUT DX,AL
        MOV DX,B_8259 ;8259 ICW2		;设置ICW2
        MOV AL,08H
        OUT DX,AL
        MOV DX,B_8259 ;8259 ICW4		;全嵌套方式，非缓冲方式，主片，自动中断结束方式，为8086-Pentium的CPU
        MOV AL,00000111B
        OUT DX,AL 
        MOV DX,B_8259 ;8259 OCW1		;M7,M6不被屏蔽
        MOV AL,00111111B
        OUT DX,AL
        STI

        CLI
        ;INTP6 -> MIR6
        MOV AX,0
        MOV ES,AX
        MOV DI,MIR6_8259
        MOV AX,OFFSET INTP6			;设置MIR6中断向量
        CLD					;将标志寄存器flag的方向标志位df清零，串操作按地址加的方式进行
        STOSW			;STOSB、STOSW 和 STOSD 指令分别将 AL/AX/EAX 的内容存入由 EDI 中偏移量指向的内存位置。EDI 根据方向标志位的状态递增或递减。
        MOV AX,SEG INTP6  		;取INTP6的段地址
        STOSW     
        ;INTP7 -> MIR7
        MOV AX,0
        MOV ES,AX
        MOV DI,MIR7_8259
        MOV AX,OFFSET INTP7			;设置MIR7中断向量
        CLD
        STOSW
        MOV AX,SEG INTP7  
        STOSW
        STI
        
        POP DX
        POP AX
        RET
INIT8259 ENDP

INITTIME PROC NEAR;初始化计时器			初始化8254，设置计时
        PUSH AX
        PUSH DX
        
        MOV DX,CON_8254;8254 CONTROL
        MOV AL,00110110B				;计数器0方式3初值MINUTE二进制，先读/写低8位
        OUT DX,AL					;计数到0结束输出正跃变信号方式，是不是与中断上升沿有关
        
        MOV DX,OUT0_8254;8254 COUNTER0
        MOV AX,MINUTE				
        OUT DX,AL 
        MOV AL,AH
        OUT DX,AL					;MINUTE=4800H，连接18.432Khz可以实现一毫秒计时
        
        MOV DX,CON_8254;8254 CONTROL
        MOV AL,01110110B				;计数器1方式3初值RETIME二进制，先读/写低8位
        OUT DX,AL					;方波
        
        MOV DX,OUT1_8254;8254 COUNTER1
        MOV AX,RETIME
        OUT DX,AL 
        MOV AL,AH
        OUT DX,AL  					;RETIME=4800H，LED刷新时间1ms
        
        POP DX
        POP AX
        RET
INITTIME ENDP

STIME PROC NEAR;开始计时				开中断			
        PUSH AX
        PUSH DX
        
        CLI
        MOV DX,B_8259 ;8259 OCW1
        MOV AL,00111111B				;打开MIR6 MIR7的中断
        OUT DX,AL
        STI
        
        MOV DX,CON8255 ;C口最高位置1
        MOV AL,00001111B				;A口输出，B口方式1输入，C口高4位低四位都是输入
        OUT DX,AL
        
        POP DX
        POP AX
        RET
STIME ENDP 

PAUSETIME PROC NEAR			;暂停计时 ，通过全屏蔽中断，然后将C口低四位作为输出，高四位输入来拒接接受数据（与之前相反）
        PUSH AX
        PUSH DX
        
        CLI
        MOV DX,B_8259 ;8259 OCW1
        MOV AL,11111111B
        OUT DX,AL
        STI
        MOV DX,CON8255 ;C口最高位置0
        MOV AL,00001110B
        OUT DX,AL
        
        POP DX
        POP AX
        RET
PAUSETIME ENDP

SHIFTNUM PROC NEAR;DNUM中数字向左移一,ANUM中数字加入最右边一 
        PUSH AX
        PUSH BX
        PUSH SI
        
        LEA BX,DNUM
        MOV SI,1
SH1:    MOV AL,SI[BX]
        MOV [SI-1][BX],AL
        INC SI
        CMP SI,TONUM
        JNZ SH1
        
        MOV AL,ANUM
        DEC SI
        MOV SI[BX],AL
        
        POP SI
        POP BX
        POP AX 
        
        RET
SHIFTNUM ENDP
       
DISPLAYS PROC NEAR ;显示DNUM中的4个数字一次					显示DUNM的内容到数码管上,DNUM中的内容是作为下标访问ledTable得到数据的
        PUSH AX
        PUSH BX
        PUSH CX
        PUSH DX
        PUSH SI
        
        LEA SI,DNUM    ;INIT DATA LEDTABLE
        LEA BX,LEDTABLE			;ledtable是从0到F，以及灭灯
D0:     MOV AH,LEFTLED;最左，选择最左边的数码管
        LEA SI,DNUM
        
D1:     MOV CX,TONUM 			 ;LED灯的个数，也就是4

D2:     MOV AL,10H;送清空段码
        XLAT					 ;[BX+AL]的内容赋给AL，即此时的AL为00H
        MOV DX,PB8255
        OUT DX,AL
        
		MOV AL,AH       ;送位选
        MOV DX,PA8255
        OUT DX,AL 
        
        MOV AL,[SI]     ;送段码
        XLAT					;获得数码管要显示的数据
        MOV DX,PB8255
        OUT DX,AL
        
        CALL DELAY
        
        ROL AH,1  ;位选码左移,选中数码管右移
        INC SI
        
        LOOP D2				 ;循环四次
        
        ;清最高一数码管
        MOV AL,10H;送清空段码
        XLAT
        MOV DX,PB8255
        OUT DX,AL                   
        
        POP SI
        POP DX
        POP CX
        POP BX
        POP AX 
        
        RET
DISPLAYS ENDP
              
              
INPUT PROC NEAR;读键盘一个数到ANUM,连续MAXR次未读到数会返回  
        PUSH AX
        PUSH BX
        PUSH CX
        PUSH DX
        
        MOV BX,MAXR;最大读取键盘次数
        
I1:     DEC BX
        JZ  I5 ;超出读取次数
        
        MOV DX,PA8255
        MOV AL,00H
        OUT DX,AL	  ;数码管全选择
        MOV DX,PC8255
        IN  AL,DX
        AND AL,0FH			;取低四位，低四位是行
        CMP AL,0FH			;低四位全一表示没有键盘输入
        JZ  I1			 ;等待按钮按下
        
        ;CALL DELAYS;消抖
        CALL DISPLAYER		;显示DNUM的内容到数码管上
        
        MOV DX,PA8255
        MOV AL,00H
        OUT DX,AL
        MOV DX,PC8255
        IN  AL,DX
        AND AL,0FH
        CMP AL,0FH
        JZ  I1			;等待输入
        
        MOV CL,0;存键号
        MOV AH,11111110B;列扫描
I2:     MOV AL,AH			;I2是为了获得列号
        MOV DX,PA8255
        OUT DX,AL			;选择数码管
        MOV DX,PC8255
        IN  AL,DX
        AND AL,0FH
        CMP AL,0FH			;检测是否有按钮按下
        JNZ I3;找到			此时CL存着的是列号
        ROL AH,1;扫描下一列
        INC CL;键号加一列
        CMP AH,11101111B		;检查四列
        JNZ I2
        JMP I1;未找到跳回I1

I3:     CMP AL,00001110B;在第0行
        JZ I4
        ADD CL,4;键号加一行 
        CMP AL,00001101B;在第1行
        JZ I4
        ADD CL,4;键号加一行
        CMP AL,00001011B;在第2行
        JZ I4
        ADD CL,4;键号加一行
        CMP AL,00000111B;在第3行
        JZ I4
        JMP I1			;列号加行号就能得到键号
        
I4:     MOV ANUM,CL;存结果		通过I2和I3可以获得需要显示的数字

        CALL DISPLAYS
        MOV DX,PA8255
        MOV AL,00H
        OUT DX,AL
        MOV DX,PC8255
        IN  AL,DX
        AND AL,0FH
        CMP AL,0FH
        JZ  I5
        JMP I4

I5:				;返回主程序
        POP DX
        POP CX
        POP BX
        POP AX 
        
        RET
INPUT ENDP

DELAYS PROC NESR 			;长延时
        PUSH CX 
        
        MOV CX,0FFFFH
        LOOP $  

        POP CX
        RET
DELAYS ENDP

DELAY PROC NEAR			;短延时
        PUSH CX 
        
        MOV CX,00F8H
        LOOP $  
        
        POP CX
        RET
DELAY ENDP 

INTP6 PROC FAR;MIR6接OUT1口每1秒减少一次数值,全嵌套方式IR6优先级高于IR7 
			;全嵌套方式是在      INIT8259   设置的
        STI
        PUSH AX
        
        CALL DTOS			;非压缩显示用BCD数字转化为计算用压缩BCD码
				;获得分和秒，将DNUM中的前两位看做分，后两位看做秒，转换为计算用压缩BCD码
        CALL SUBBCD			;更新分和秒
        CALL STOD 			;和DTOS是互逆的过程
        
        MOV AL,0[SNUM]
        CMP AL,00H			
        JNZ TZ			;如果分钟不变为0跳转
        MOV AL,1[SNUM]
        CMP AL,00H 
        JNZ TZ			;如果秒不变为0跳转
               
TIMEUP: CALL FLASH_3_TIMES		;说明分钟与秒都为0，要闪烁三次
        POP AX
        POP WORD PTR TIMERET
        POP WORD PTR TIMERET+2
        ;重新开始。
        PUSH COD			;CS?
        PUSH OFFSET S0
        IRET
      
TZ:     POP AX
        IRET
INTP6 ENDP

INTP7 PROC FAR;MIR7接OUT0口每0.001秒刷新一次LED
        STI				;开中断
        ;CALL DISPLAYS
        
        IRET
INTP7 ENDP

DISPLAYER PROC NEAR			;显示DNUM并延时
        PUSH CX
        MOV CX,1FH
DDD1:
        CALL DISPLAYS
        CALL DELAY
        LOOP DDD1
        POP CX
        RET
DISPLAYER ENDP

STOD PROC NEAR ;计算用压缩BCD码转化为非压缩显示用BCD数字	SNUM（用于计算）中的数字转换到DNUM（用于显示）
        PUSH AX
        PUSH CX
        PUSH SI
        PUSH DI 
        MOV CL,4   
        
        MOV SI,0
        MOV DI,0
STOD0:  MOV AL,SI[SNUM]
        AND AL,0F0H
        SHR AL,CL				;逻辑左移四位
        MOV DI[DNUM],AL
        MOV AL,SI[SNUM]
        AND AL,0FH
        MOV DI+1[DNUM],AL
        INC SI
        INC DI
        INC DI
        CMP DI,TONUM-1
        JNA STOD0
        
;        MOV DI,0 ;将前几位为零的数字转化为不显示的字符10H
;STOD2:  CMP DI[DNUM],00H
;        JNZ STOD1
;        CMP DI,TONUM-1
;        JZ  STOD1
;        MOV DI[DNUM],10H
;        INC DI
;        JMP STOD2    
STOD1:        
        POP DI
        POP SI
        POP CX
        POP AX
        RET
STOD ENDP

DTOS PROC NEAR ;非压缩显示用BCD数字转化为计算用压缩BCD码	将DNUM（显示）中的数字转换到SNUM（计算）
				;获得分和秒，将DNUM中的前两位看做分，后两位看做秒，转换为计算用压缩BCD码
        PUSH AX
        PUSH CX
        PUSH SI
        PUSH DI
        MOV CL,4

        MOV SI,0
        MOV DI,0       
DTOS0:  MOV AL,DI[DNUM]
        AND AL,0FH
        SHL AL,CL			;逻辑左移四位
        MOV AH,DI+1[DNUM]
        AND AH,0FH
        OR AL,AH
        MOV SI[SNUM],AL
        INC SI
        INC DI
        INC DI
        CMP DI,TONUM-1
        JNA DTOS0			;无符号数： 大于 ja   不大于  jna，有符号数： 大于jg  小于等于  jle
        
        POP DI
        POP SI
        POP CX
        POP AX
        RET
DTOS ENDP

SUBBCD PROC NEAR ;秒位60进制压缩BCD码减法		SNUM的递减计算
        PUSH AX
        
        MOV AL,1[SNUM]			;获得秒数
        SUB AL,1
        DAS  				;DAS（减法后的十进制调整）指令将其转换为压缩十进制格式。
        CMP AL,99H
        JNZ SUBBCDNO 	
        MOV AL,59H				;AL借位后-1会变为99H，此时将AL赋值为59，这样就实现了分秒的进制
        STC ;有借位				;CF位置1，表示有借位，因为不是正常的借位，所以要强制修改CF
        JMP SUBBCDNEXT
SUBBCDNO:
        CLC ;无借位				;清除CF位，表示无借位
SUBBCDNEXT:
        MOV 1[SNUM],AL			;把AL赋值给秒
        MOV AL,0[SNUM]			;获得分
        SBB AL,00H				;sbb是带借位减法指令,它利用了CF位上记录的借位值
        DAS					;DAS(减法后的十进制调整)指令将其转换为压缩十进制格式
        MOV 0[SNUM],AL			;将AL赋值给分
        
        POP AX
        RET
SUBBCD ENDP

COD ENDS   
    END START